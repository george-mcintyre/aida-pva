#! /bin/tcsh -f
#
# Abs:   This script populates the Aida Oracle database tables for
#        the Aida utility server.
#
# Name:  slc_utility_populate_aida_database.sh  
#
# Rem:   After a database install, the VMS command script
#        slccom:slcdb2oracle.submit is invoked, which among other
#        things dumps trigger device name information into the file
#        NFS_NDB:trignames.dmp.  The VMS NFS_NDB area is mapped
#        to the UNIX /mccdev/nfs_slcdb area, which is visible
#        from such UNIX machines as slcs2.  Later, this VMS
#        command script invokes the UNIX run_pmu_load.sh script
#        on the slcs2 machine using the command server mechanism.
#        The UNIX run_pmu_load.sh script invokes this script.
#
#  Facility:  SLAC
#
#  Auth: 27-Jul-2007, Bob Hall (rdh)
#  Rev:  dd-mmm-yyyy, Reviewer's Name    (USERNAME)
#--------------------------------------------------------------
#
#  Mod:
#        04-Sep-2012, brobeck
#           Changed rdh mail to controls-system-admins
#        14-May-2012, Greg White (greg)
#           Changed Oracle password.
#        20-Sep-2011, Bob Hall (rdh)
#           Changed Oracle password.
#        04-Apr-2011, Bob Hall (rdh)
#           Changed Oracle password.
#        04-Apr-2011, Bob Hall (rdh)
#           Changed Oracle password.
#        15-Sep-2010, Bob Hall (rdh)
#           Changed Oracle password.
#        23-Mar-2010, Bob Hall (rdh)
#           Changed Oracle password.
#        26-Aug-2009, Bob Hall (rdh)
#           Changed Oracle password.
#        23-Apr-2009, Bob Hall (rdh)
#           Modified to generate set_aida_slc_utility log
#           files in /nfs/slac/g/cd/log/aidadb.  Also modified to
#           send mail upon the completion of running
#           set_aida_slc_utility.sql for the aidaprod account.
#        09-Mar-2009, Bob Hall (rdh)
#           Changed Oracle password.
#        25-Aug-2008, Bob Hall (rdh)
#           Changed Oracle password.
#        13-Feb-2008, Bob Hall (rdh)
#           Changed Oracle password.
#
#==============================================================
#
#
source /afs/slac/g/cd/soft/dev/script/ENVS.csh
source $CD_SOFT/dev/script/oracle_env.csh
setenv TWO_TASK SLACPROD

caddpath ORACLE_PATH ${CD_SOFT}/tst/ora
caddpath ORACLE_PATH ${CD_SOFT}/dev/ora
setenv ORACLE_HOME /afs/slac/package/oracle/f/11.1.0/sun4x_59/11.1.0
setenv PATH $ORACLE_HOME/bin:${PATH}

#
# First copy the trignames.dmp file into the
# /nfs/slac/g/cd/oracle/aida directory.
#
cp /mccdev/nfs_slcdb/trignames.dmp /nfs/slac/g/cd/oracle/aida

#
# Run the create_aida_utility_load_file.pl Perl script to use information
# in the trignames.dmp file to create the file
# /nfs/slac/g/cd/oracle/aida/aida_utility_load.dat. This file contains
# the Aida instance/attribute pairs for the trigger deactivate/reactivate
# and get activation status functionality of the Aida utility server.
# It also contains the "MKB/VAL" instance/attribute pair for the multiknob
# functionality of the Aida utility server.
#
create_aida_utility_load_file.pl

#
# Invoke the set_aida_slc_utility.sql PL/SQL script to read the file containing
# instance/attribute pairs generated by the create_aida_utility_load_file.pl 
# Perl script and populate the appropriate tables in the Aida aidadev
# Oracle account.
#
set search_string = "errcode: 0"

set logfname = /nfs/slac/g/cd/log/aidadb/set_aida_slc_utility_aidadev.log

setenv TNS_ADMIN /afs/slac/g/lcls/tools/oracle/wallets/aidadev
sqlplus /@$TWO_TASK @set_aida_slc_utility.sql >> ${logfname}
#####sqlplus aidadev/gver3d_i @set_aida_slc_utility.sql >> ${logfname}
tail -6 ${logfname} | grep "$search_string"'$'
if ( $status != 0 ) then
    echo set_aida_slc_utility.sql failed for aidadev. >>! /tmp/set_aida_slc_utility_aidadev.log
    echo check ${logfname}
    echo set_aida_slc_utility.sql failed for aidadev | Mail -s "aidadev set_aida_slc_utility.sql failed" controls-system-admins@slac.stanford.edu
    exit 1
endif

#
# Populate the Aida production Oracle database with SLC utility
# file PV information in the same manner as was done for the Aida
# development Oracle database.
#
set logfname = /nfs/slac/g/cd/log/aidadb/set_aida_slc_utility_aidaprod.log
setenv TNS_ADMIN /afs/slac/g/lcls/tools/oracle/wallets/aidaprod
sqlplus /@$TWO_TASK @set_aida_slc_utility.sql >> ${logfname}
####sqlplus aidaprod/gver3d_i @set_aida_slc_utility.sql >> ${logfname}
tail -6 ${logfname} | grep "$search_string"'$'
if ( $status != 0 ) then
    echo set_aida_slc_utility.sql failed for aidaprod. >>! /tmp/set_aida_slc_utility_aidaprod.log
    echo check ${logfname}
    echo set_aida_slc_utility.sql failed for aidaprod | Mail -s "aidaprod set_aida_slc_utility.sql failed" controls-system-admins@slac.stanford.edu
    exit 1
else
    echo set_aida_slc_utility.sql aidaprod finished | Mail -s "aidaprod set_aida_slc_utility.sql completed" controls-system-admins@slac.stanford.edu
endif

#
# Populate the Aida LCLS MCCQA Oracle database with SLC utility
# file PV information in the same manner as was done for the Aida
# development Oracle database.
#
setenv TWO_TASK MCCQA
set logfname = /nfs/slac/g/cd/log/aidadb/set_aida_slc_utility_aidalcls.log
setenv TNS_ADMIN /afs/slac/g/lcls/tools/oracle/wallets/aida
sqlplus /@$TWO_TASK @set_aida_slc_utility.sql >> ${logfname}
####sqlplus aida/gver3d_i @set_aida_slc_utility.sql >> ${logfname}
tail -6 ${logfname} | grep "$search_string"'$'
if ( $status != 0 ) then
    echo set_aida_slc_utility.sql failed for aidalcls. >>! /tmp/set_aida_slc_utility_aidalcls.log
    echo check ${logfname}
    echo set_aida_slc_utility.sql failed for aidalcls | Mail -s "aidalcls set_aida_slc_utility.sql failed" controls-system-admins@slac.stanford.edu
    exit 1
else
    echo set_aida_slc_utility.sql aidalcls finished | Mail -s "aidalcls set_aida_slc_utility.sql completed" controls-system-admins@slac.stanford.edu
endif
